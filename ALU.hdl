// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    Mux16(a= x, b=false, sel= zx, out= out1); // if (zx == 1) sets x = 0
    Not16(in= out1, out= notX);
    Mux16(a= out1, b= notX, sel= nx, out= finalX);    // if (nx == 1) sets x = !x

    Mux16(a= y, b=false, sel= zy, out= out2); // if (zy == 1) sets y = 0
    Not16(in= out2, out= notY);
    Mux16(a= out2, b= notY, sel= ny, out= finalY);    // if (ny == 1) sets y = !y

    Add16(a= finalX, b= finalY, out= addXY);
    And16(a= finalX, b= finalY, out= andXY);
    Mux16(a= andXY, b= addXY, sel= f, out= out3); // if (f == 1)  sets out = x + y ELSE out = x & y

    Not16(in= out3, out= notOut);
    Mux16(a= out3, b= notOut, sel= no, out= out,  out[15]=firstOut, out[0..7]=finalLeft, out[8..15]=finalRight); // if (no == 1) sets out = !out

    // output == 0 (zr)
      Or8Way(in=finalLeft, out=zrl);
      Or8Way(in=finalRight, out=zrr);
      Or(a=zrl, b=zrr, out=nzr);
      Not(in=nzr, out=zr);

    And(a=firstOut, b=true, out=ng); // if (out < 0)  ng = 1, else ng = 0
}